struct PlaneUniforms {
  mvp: mat4x4f,
  opacity: f32,
  forceOpaqueAlpha: f32,
  hasMask: f32,
  maskInvert: f32,
}

struct VertexOutput {
  @builtin(position) position: vec4f,
  @location(0) uv: vec2f,
}

@group(0) @binding(0) var texSampler: sampler;
@group(0) @binding(1) var inputTexture: texture_2d<f32>;
@group(0) @binding(2) var<uniform> plane: PlaneUniforms;
@group(0) @binding(3) var maskTexture: texture_2d<f32>;

@vertex
fn vertexMain(@builtin(vertex_index) vertexIndex: u32) -> VertexOutput {
  var positions = array<vec2f, 6>(
    vec2f(-0.5, -0.5),
    vec2f(0.5, -0.5),
    vec2f(-0.5, 0.5),
    vec2f(-0.5, 0.5),
    vec2f(0.5, -0.5),
    vec2f(0.5, 0.5),
  );

  var uvs = array<vec2f, 6>(
    vec2f(0.0, 1.0),
    vec2f(1.0, 1.0),
    vec2f(0.0, 0.0),
    vec2f(0.0, 0.0),
    vec2f(1.0, 1.0),
    vec2f(1.0, 0.0),
  );

  var output: VertexOutput;
  output.position = plane.mvp * vec4f(positions[vertexIndex], 0.0, 1.0);
  output.uv = uvs[vertexIndex];
  return output;
}

@fragment
fn fragmentMain(input: VertexOutput) -> @location(0) vec4f {
  let color = textureSample(inputTexture, texSampler, input.uv);
  let sourceAlpha = select(color.a, 1.0, plane.forceOpaqueAlpha > 0.5);
  var maskValue = 1.0;
  if (plane.hasMask > 0.5) {
    maskValue = textureSample(maskTexture, texSampler, input.uv).r;
    if (plane.maskInvert > 0.5) {
      maskValue = 1.0 - maskValue;
    }
  }
  let alpha = sourceAlpha * plane.opacity * maskValue;
  if (alpha < 1.0 / 255.0) {
    discard;
  }
  return vec4f(color.rgb, alpha);
}
